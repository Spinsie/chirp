plugins {
	id 'application'
	id 'maven-publish'
	id 'signing'
	id 'jacoco'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.sonarsource.sonarlint.core:sonarlint-core:5.3.1.31327'
	testImplementation 'junit:junit:4.13.2'
	testImplementation 'com.google.code.gson:gson:2.8.6'
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.enabled true
		html.enabled true
	}
}

task calculateCoverage {
	dependsOn jacocoTestReport
	doLast {
		if (System.env('CI') == null) {
			return
		}
		def slurper = new XmlSlurper()
		slurper.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false)
		slurper.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false)
		def reportFile = file("$buildDir/reports/jacoco/test/jacocoTestReport.xml")
		def report = slurper.parse(reportFile)
		def coverages = report.counter.find { it.'@type' == 'INSTRUCTION' }
		double missed = coverages.@missed.toDouble()
		double covered = coverages.@covered.toDouble()
		def coverage = (covered / (missed + covered) * 100).round(1)
		exec {
			commandLine 'export', "COVERAGE=$coverage"
			commandLine 'export', 'COVERAGE_COLOR=:brightgreen'
		}
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
	withJavadocJar()
	withSourcesJar()
}

application {
	mainClass = 'io.github.spinsie.chirp.Analyzer'
}

version = {
	def stdout = new ByteArrayOutputStream()
	def result = exec {
		commandLine 'git', 'describe', '--tags', '--match', 'v[0-9]*'
		standardOutput = stdout
		ignoreExitValue true
	}
	if (result.getExitValue() != 0) {
		return 'unspecified'
	}
	return stdout.toString().trim().substring(1)
}()

jar {
	manifest {
		attributes 'Implementation-Title': project.name,
			'Implementation-Version': project.version,
			'Main-Class': 'io.github.spinsie.chirp.Analyzer',
			'Sealed': 'true'
	}
}

signing {
	required = { gradle.taskGraph.hasTask('uploadArchives') }
	sign publishing.publications
}

tasks.withType(Jar) {
	from('LICENSE') {
		into 'META-INF'
	}
}

publishing {
	repositories {
		maven {
			url = { 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/' }
			credentials {
				username = findProperty('ossrhUsername')
				password = findProperty('ossrhPassword')
			}
		}
	}
	publications {
		sonatype(MavenPublication) {
			from components.java
			pom {
				name = 'chirp'
				description = 'Library and CLI Code Analyzer'
				url = 'https://github.com/spinsie/chirp'
				scm {
					connection = 'scm:git:git://github.com/spinsie/chirp.git'
					developerConnection = 'scm:git:ssh://github.com/spinsie/chirp.git'
					url = 'https://github.com/spinsie/chirp'
				}
				licenses {
					license {
						name = 'MIT License'
						url = 'https://github.com/spinsie/chirp/blob/main/LICENSE'
					}
				}
				developers {
					developer {
						name = 'spinsie'
						email = '009101@gmail.com'
					}
				}
			}
		}
	}
}